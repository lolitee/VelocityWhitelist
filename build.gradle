plugins {
    id 'java'
    id 'eclipse'
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.0.1"
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

group = 'com.lolitee'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        url "https://m2.dv8tion.net/releases"
    }
}

dependencies {
    compileOnly 'com.velocitypowered:velocity-api:3.1.2-SNAPSHOT'
    annotationProcessor 'com.velocitypowered:velocity-api:3.1.2-SNAPSHOT'
    implementation('net.dv8tion:JDA:5.0.0-beta.2') {
        exclude module: 'opus-java'
    }

    implementation 'mysql:mysql-connector-java:8.0.30'
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.1.0'
}

def targetJavaVersion = 11
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

def templateSource = file('src/main/templates')
def templateDest = layout.buildDirectory.dir('generated/sources/templates')
def generateTemplates = tasks.register('generateTemplates', Copy) { task ->
    def props = [
            'version': project.version
    ]
    task.inputs.properties props

    task.from templateSource
    task.into templateDest
    task.expand props
}

sourceSets.main.java.srcDir(generateTemplates.map { it.outputs })

rootProject.idea.project.settings.taskTriggers.afterSync generateTemplates
project.eclipse.synchronizationTasks(generateTemplates)

shadowJar {
    dependencies {
//        include dependency("mysql:mysql-connector-java")
//        include dependency("net.dv8tion:JDA")
//        include dependency("gnu.trove-map:TLongObjectMap")
        relocate 'net.dv8tion', 'com.lolitee.libs.net.dv8tion'
        relocate 'com.mysql', 'com.lolitee.libs.com.mysql'
        relocate 'gnu.trove', 'com.lolitee.libs.gnu.trove'
    }
}